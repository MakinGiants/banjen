plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

apply from: '../jacoco.gradle'

def appId = "com.makingiants.android.banjotuner"
def appVersionName = "1.7.0"
def appVersionCode = 121

def customProperty(key) {
    if (project.hasProperty(key)) {
        return project.property(key)
    } else {
        return System.getenv(key)
    }
}

android {
    namespace appId
    compileSdk setup.targetSdk

    defaultConfig {
        applicationId appId
        minSdk setup.minSdk
        targetSdk setup.targetSdk
        versionCode appVersionCode
        versionName appVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue "string", "ads_unit_id_banner", "\"${customProperty("BANJEN_ADS_UNIT_ID_BANNER")}\""
        resValue "string", "ads_app_id", "\"${customProperty("BANJEN_ADMOB_APP_ID")}\""

        // Enable just the defined languages allowing to shrink apk size more
        resConfigs "en", "pt", "es", "it"
    }

    signingConfigs {
        if (project.hasProperty('BANJEN_SIGN_PATH')) {
            release {
                storeFile file(customProperty("BANJEN_SIGN_PATH"))
                storePassword customProperty("BANJEN_SIGN_STORE_PASSWORD")
                keyAlias customProperty("BANJEN_SIGN_KEY_ALIAS")
                keyPassword customProperty("BANJEN_SIGN_KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'rules-test-e2e.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true

            if (project.hasProperty('BANJEN_SIGN_PATH')) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'rules.pro'
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    lint {
        abortOnError false
    }

    buildFeatures {
        compose true
        resValues true
    }

}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:2.3.10"
    implementation "androidx.appcompat:appcompat:1.7.1"
    implementation "com.google.android.gms:play-services-ads-lite:24.8.0"

    implementation platform("com.google.firebase:firebase-bom:34.9.0")
    implementation "com.google.firebase:firebase-crashlytics"
    implementation 'com.google.firebase:firebase-analytics'

    def composeBom = platform('androidx.compose:compose-bom:2026.02.00')
    implementation composeBom
    androidTestImplementation composeBom
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material-icons-extended'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.activity:activity-compose:1.12.4'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:2.3.10"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:2.3.10"

    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation "androidx.test.ext:junit:1.3.0"
    androidTestImplementation 'androidx.test:runner:1.7.0'
    androidTestImplementation 'androidx.test:rules:1.7.0'
}
