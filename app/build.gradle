plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

apply from: '../jacoco.gradle'

def appId = "com.makingiants.android.banjotuner"
def appVersionName = "1.6.0"
def appVersionCode = 119

def customProperty(key) {
    if (project.hasProperty(key)) {
        return project.property(key)
    } else {
        return System.getenv(key)
    }
}

android {
    namespace appId
    compileSdk setup.targetSdk

    defaultConfig {
        applicationId appId
        minSdk setup.minSdk
        targetSdk setup.targetSdk
        versionCode appVersionCode
        versionName appVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue "string", "ads_unit_id_banner", "\"${customProperty("BANJEN_ADS_UNIT_ID_BANNER")}\""
        resValue "string", "ads_app_id", "\"${customProperty("BANJEN_ADMOB_APP_ID")}\""

        // Enable just the defined languages allowing to shrink apk size more
        resConfigs "en", "pt", "es", "it"
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'rules-test-e2e.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'rules.pro'
        }
    }

    signingConfigs {
        release {
            if (project.hasProperty('BANJEN_SIGN_PATH')) {
                storeFile file(project.property("BANJEN_SIGN_PATH"))
                storePassword project.property("BANJEN_SIGN_PWD")
                keyAlias project.property("BANJEN_SIGN_ALIAS")
                keyPassword project.property("BANJEN_SIGN_PWD")
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    lintOptions {
        abortOnError false
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.2"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.9.0"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "com.google.android.gms:play-services-ads-lite:22.3.0"

    implementation platform("com.google.firebase:firebase-bom:32.2.2")
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation 'com.google.firebase:firebase-analytics'

    def composeBom = platform('androidx.compose:compose-bom:2023.08.00')
    implementation composeBom
    androidTestImplementation composeBom
    implementation 'androidx.compose.material3:material3-android:1.2.0-alpha06'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.activity:activity-compose:1.8.0-alpha07'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:1.9.0"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:1.9.0"

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
}
