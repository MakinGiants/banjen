apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.firebase.crashlytics'

apply from: '../jacoco.gradle'

def customProperty(key) {
    if (project.hasProperty(key)) {
        return project.property(key)
    } else {
        return System.getenv(key)
    }
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.makingiants.android.banjotuner"
        minSdk 19
        targetSdk 31
        versionCode 119
        versionName "1.6.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Enable just English configs for dependencies, allowing to shrink apk size more
        resConfigs "en", "pt"

        resValue "string", "ads_unit_id_banner", "\"${customProperty("BANJEN_ADS_UNIT_ID_BANNER")}\""
        resValue "string", "ads_app_id", "\"${customProperty("BANJEN_ADMOB_APP_ID")}\""
    }

    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources true
            testCoverageEnabled = true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'rules-test-e2e.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/services/org.xmlpull.v1.XmlPullParserFactory'
    }

    signingConfigs {
        release {
            if (project.hasProperty('BANJEN_SIGN_PATH')) {
                storeFile file(project.property("BANJEN_SIGN_PATH"))
                storePassword project.property("BANJEN_SIGN_PWD")
                keyAlias project.property("BANJEN_SIGN_ALIAS")
                keyPassword project.property("BANJEN_SIGN_PWD")
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    //implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$deps.kotlin"

    implementation "androidx.appcompat:appcompat:1.4.0"
    implementation "com.google.android.gms:play-services-ads-lite:20.5.0"
    implementation 'com.google.firebase:firebase-analytics:20.0.2'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.6'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$deps.kotlin"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$deps.kotlin"

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
}
